/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


description = 'Reactor-based application framework for the JVM.'

ext {
	gradleVersion = '1.7'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.1.6'

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	// Libraries
	disruptorVersion = '3.2.0'
	springVersion = '3.2.3.RELEASE'
	siVersion = '3.0.0.M2'
	nettyVersion = '4.0.6.Final'
	jacksonDatabindVersion = '2.2.2'
	hadoopVersion = '1.1.2'
	javaChronicleVersion = '1.7.2'
    openHftChronicleVersion = '2.0.1'

	// Testing
	mockitoVersion = '1.9.5'
	spockVersion = '0.7-groovy-2.0'

	// Code coverage
	jacocoVersion = '0.6.2.201302030002'

	javadocLinks = [
			"http://docs.oracle.com/javase/7/docs/api/",
			"http://docs.oracle.com/javaee/6/api/"
	] as String[]
}

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
	}
}
apply from: "$gradleScriptDir/setup.gradle"

configure(allprojects) { project ->
	group = 'org.projectreactor'

	apply plugin: 'propdeps'
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply from: "${gradleScriptDir}/ide.gradle"

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:-options",     // intentionally disabled
			"-Xlint:-fallthrough", // intentionally disabled
			"-Xlint:-rawtypes",    // TODO enable and fix warnings
			"-Xlint:-deprecation", // TODO enable and fix warnings
			"-Xlint:-unchecked"    // TODO enable and fix warnings
	]

	compileGroovy{
		sourceCompatibility = 1.6
		targetCompatibility = 1.6
	}

	compileJava {
		sourceCompatibility = 1.6
		targetCompatibility = 1.6
	}

	compileTestJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
		//compileTestJava.options.compilerArgs += "-parameters"
	}

	sourceSets.test.resources.srcDirs = ["src/test/resources", "src/test/java"]

	configurations {
		jacoco
	}

	configurations.all {
		exclude group: 'commons-logging'
		exclude module: 'junit'
	}

	test {
		systemProperty("java.awt.headless", "true")
		systemProperty("testGroups", project.properties.get("testGroups"))
		scanForTestClasses = false
		include '**/*Tests.*'
		exclude '**/*Abstract*.*'
	}

	repositories {
		maven { url 'http://repo.spring.io/libs-milestone' }
		//mavenLocal()
		mavenCentral()
	}

	// dependencies that are common across all java projects
	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"

		// JSR-305 annotations
        optional "com.google.code.findbugs:jsr305:2.0.0"

		// Groovy
		testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

		// Testing
		testCompile "org.spockframework:spock-core:$spockVersion",
				"org.hamcrest:hamcrest-library:1.3"
		testRuntime "ch.qos.logback:logback-classic:$logbackVersion"

		// Code coverage
		jacoco "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"
	}

	ext.javadocLinks = [
			"http://docs.oracle.com/javase/7/docs/api/",
			"http://docs.oracle.com/javaee/6/api/",
			"http://fasterxml.github.com/jackson-core/javadoc/2.2.0/"
	] as String[]
}

configure(subprojects - project(":reactor-benchmark")) { subproject ->
	jar {
		manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
		manifest.attributes["Implementation-Title"] = subproject.name
		manifest.attributes["Implementation-Version"] = subproject.version
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task groovydocJar(type: Jar) {
		classifier = 'groovydoc'
		from groovydoc
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
		archives groovydocJar
	}

	test {
		testLogging {
			jvmArgs "-javaagent:${configurations.jacoco.asPath}=destfile=${buildDir}/jacoco.exec,includes=reactor.*"
			events "failed"
			exceptionFormat "full"
		}
	}
}

configure(rootProject) {
	description = "Reactor"

	configurations.archives.artifacts.clear()

	task api(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"

		dependsOn {
			subprojects.collect {
				it.tasks.getByName("jar")
			}
		}
		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		//options.overview = "src/api/overview.html"
		options.stylesheetFile = file("src/api/stylesheet.css")
		options.links(project.ext.javadocLinks)

		source subprojects.collect { project ->
			project.sourceSets.main.allJava
		}

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "api")

		doFirst {
			classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
		}
	}
}

project('reactor-core') {
	description = 'Core Reactor components'

	apply plugin: 'osgi'

	dependencies {
		// Type-1 UUIDs
		compile "com.eaio.uuid:uuid:3.2"

		// High-speed Dispatching
		compile "com.lmax:disruptor:$disruptorVersion"

		// High-speed Messaging
		//optional("com.higherfrequencytrading:chronicle:$javaChronicleVersion")
        optional "net.openhft:chronicle:$openHftChronicleVersion"
	}
}

project('reactor-groovy') {
	description = 'Reactor Groovy components'

	apply plugin: 'osgi'

	sonarRunner {
		sonarProperties {
			property "sonar.language", "grvy"
		}
	}

	dependencies {
		compile project(':reactor-core')
		compile "org.codehaus.groovy:groovy-all:$groovyVersion"
	}
}

project('reactor-spring') {
	apply plugin: 'osgi'
	description = 'Reactor Spring components'

	eclipse {
		project {
			natures += 'org.springframework.ide.eclipse.core.springnature'
		}
	}

	dependencies {
		compile project(':reactor-core')

		// Spring
		compile "org.springframework:spring-beans:$springVersion",
				"org.springframework:spring-context:$springVersion",
				"org.springframework:spring-context-support:$springVersion",
				"org.springframework:spring-core:$springVersion",
				"org.springframework:spring-expression:$springVersion",
				"org.springframework:spring-tx:$springVersion"

		// Testing
		testCompile "org.spockframework:spock-spring:$spockVersion",
				"org.springframework:spring-test:$springVersion"
		testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	}
}

project('reactor-logback') {
	description = 'Processor-based Logback async appender implementation'
	dependencies {
		compile project(':reactor-core')

		// Logback
		compile "ch.qos.logback:logback-classic:$logbackVersion"
	}
}

project('reactor-benchmark') {
	description = 'Performance tests'

	sonarRunner {
		skipProject = true
	}

	dependencies {
		compile project(':reactor-core')
	}
}

project('reactor-tcp') {
	apply plugin: 'osgi'
	description = 'Reactor TCP components'

	ext.bundleImportPackages = [
			'com.fasterxml.jackson.core;resolution:=optional',
			'com.fasterxml.jackson.databind;resolution:=optional',
			'*'
	]

	dependencies {
		compile project(':reactor-core')

		compile "io.netty:netty-all:$nettyVersion"
		optional("com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion")

		// Testing
		testCompile "org.apache.hadoop:hadoop-client:$hadoopVersion"
		testRuntime project(':reactor-logback'),
				"org.slf4j:jcl-over-slf4j:$slf4jVersion"
	}

	jar {
		manifest {
			instruction 'Import-Package', bundleImportPackages.join(',')
		}
	}
}

task wrapper(type: Wrapper, description: "Create a Gradle self-download wrapper") {
	group = 'Project Setup'
	gradleVersion = "$gradleVersion"
}
